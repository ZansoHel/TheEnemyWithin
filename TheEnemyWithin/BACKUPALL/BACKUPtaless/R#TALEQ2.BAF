IF
	Global("TEW_caravanquest","GLOBAL",7)
THEN
	RESPONSE #100
		Wait(5)
		DestroySelf()
END

IF
	!Global("TEW_caravanquest","GLOBAL",6)
	Global("TALES2_DACT","GLOBAL",0)
THEN
	RESPONSE #100
		Deactivate(Myself)
		SetGlobal("TALES2_DACT","GLOBAL",1)
END

IF
	Global("TEW_caravanquest","GLOBAL",6)
	Global("TALES2_ACT","GLOBAL",0)
THEN
	RESPONSE #100
		Activate(Myself)
		JumpToPoint([2546.1506])
		SetGlobal("TALES2_ACT","GLOBAL",1)
END

IF
	Global("TALESS_DEAD","GLOBAL",1)
THEN
	RESPONSE #100
		DestroySelf()
END

IF
	Die()	
THEN
	RESPONSE #100
		SetGlobal("TALESS_DEAD","GLOBAL",1)	
END

IF
	GlobalTimerExpired("TALES2_NIGHTT","GLOBAL")
	Global("TALES2_NIGHTB","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("TALES2_NIGHTA","GLOBAL",0)
		SetGlobal("TALES2_NIGHTB","GLOBAL",1)
END

IF
	AreaCheck("AR0700")
	Global("TALES2_TELE","GLOBAL",1)	
THEN
	RESPONSE #100
		JumpToPoint([2546.1506])
		SetGlobal("TALESS_TAIL2","GLOBAL",0)
		SetGlobalTimer("TALES2_TELET","GLOBAL",EIGHT_HOURS)
		SetGlobal("TALES2_TELE","GLOBAL",2)
		Deactivate(Myself)
END

IF
	AreaCheck("AR0700")
	TimeOfDay(NIGHT)
	Global("TEW_caravanquest","GLOBAL",6)
	!GlobalTimerNotExpired("TALES2_TELET","GLOBAL")
	Global("TALES2_NIGHTA","GLOBAL",0)
THEN
	RESPONSE #100
		JumpToPoint([2546.1506])
		Activate(Myself)
		Face(N)
		SetGlobal("TALES2_TELE","GLOBAL",0)
		SetGlobal("TALES2_NIGHTA","GLOBAL",1)
		SetGlobal("TALES2_NIGHTB","GLOBAL",0)
		SetGlobalTimer("TALES2_NIGHTT","GLOBAL",EIGHT_HOURS)
END

IF
	AreaCheck("AR0700")
	TimeOfDay(NIGHT)
	See([PC])
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@2000100) // I will not be followed by nosy adventurers! My business is mine and mine alone!
		SetGlobal("TALESS_TAIL2","GLOBAL",0)
		SetGlobal("TALES2_TELE","GLOBAL",1)
		Wait(1)
		ForceSpell(Myself,WIZARD_DIMENSION_DOOR) // SPWI402.SPL (Dimension Door)
		Wait(2)
		JumpToPoint([2546.1506])
END

IF
	AreaCheck("AR0700")
	!See([PC])
	Range(Nearest([PC]),30)
	TimeOfDay(NIGHT)
	Global("TALESS_TAIL1D","GLOBAL",1)
	Global("TALESS_TAIL2D","GLOBAL",0)
	Global("TALESS_TAIL2","GLOBAL",0)
	Global("TEW_caravanquest","GLOBAL",6)
	Global("TALES2_TELE","GLOBAL",0)
THEN
	RESPONSE #100
		Wait(1)
		DisplayStringHead(Myself,@2000126) // A bit chilly tonight.
		MoveToPoint([1214.1986])
		Wait(1)
		Face(N)
		Wait(1)
		Face(NNE)
		Wait(1)
		Face(NE)
		Wait(1)
		DisplayStringHead(Myself,@2000109) // I thought I heard something.
		IncrementGlobal("TALESS_TAIL2","GLOBAL",1)
END

IF
	AreaCheck("AR0700")
	!See([PC])
	Range(Nearest([PC]),30)
	TimeOfDay(NIGHT)
	Global("TALESS_TAIL2","GLOBAL",1)
	Global("TEW_caravanquest","GLOBAL",6)
THEN
	RESPONSE #100
		Wait(1)
		MoveToPoint([1228.3097])
		Wait(3)
		IncrementGlobal("TALESS_TAIL2","GLOBAL",1)
END

IF
	AreaCheck("AR0700")
	!See([PC])
	Range(Nearest([PC]),30)
	TimeOfDay(NIGHT)
	Global("TALESS_TAIL2","GLOBAL",2)
	Global("TEW_caravanquest","GLOBAL",6)
THEN
	RESPONSE #100
		Activate("R#THIEF1")
		Activate("R#THIEF2")
		MoveToPoint([2247.2503])
		Wait(3)
		IncrementGlobal("TALESS_TAIL2","GLOBAL",1)
		StartCutSceneMode()
		StartCutScene("R#TALEC2")
END

